?start: [(statement)+]
?statement: expression ";"                   -> set_distance
          | "$" VALNAME "=" parameter ";"    -> set_variable
          | (mapobject ".")+ mapfunc ";"     -> map_element
          | ("include"|"Include") expression ";"         -> include_file
          | ";"                              -> null_statement
      
?parameter: expression

?expression: term
           | expression "+" term -> add
           | expression "-" term -> sub
           | "null"               -> null_value

?term: factor
     | term "*" factor -> mul
     | term "/" factor -> div
     | term "%" factor -> mod

?factor: SIGNED_NUMBER          -> number
       | "-" factor             -> neg
       | "+" factor             -> pos
       | string
       | "(" expression ")"
       | "$" VALNAME            -> call_variable
       | label                  -> call_predefined_variable
       | label "(" [expression ("," expression)*] ")" -> call_function
          
?mapfunc: label "(" [expression] ("," [expression])* ")"
        
?mapobject: label ["[" parameter "]"] // [key]が指定されていない場合もNoneを返す

?label: CNAME
VALNAME: ("_"|LETTER|SIMCH_LETTER|DIGIT) ("_"|LETTER|SIMCH_LETTER|DIGIT)*

string: /'[^']*'/ -> remquote

SIMCH_LETTER: ("\u0080".."\u07ff") | ("\u0800".."\uffff") | ("\U00010000".."\U0010ffff")

%import common.CNAME
%import common.LETTER
%import common.DIGIT
%import common.SIGNED_NUMBER

%import common.WS_INLINE
%import common.SH_COMMENT
%import common.CPP_COMMENT
%import common.NEWLINE

%ignore WS_INLINE
%ignore SH_COMMENT
%ignore CPP_COMMENT
%ignore NEWLINE
%ignore "\""
%ignore "\xa0" // 半角カナのスペース
%ignore "\u3000" //ユニコードの全角スペース
%ignore "\x8140" //sjisの全角スペース
