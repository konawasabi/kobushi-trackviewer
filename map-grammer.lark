?start: [(statement)+]
?statement: expression ";"                   -> set_distance
          | "$" VALNAME "=" parameter ";"      -> set_variable
          | (mapobject ".")+ mapfunc ";"     -> map_element
          | "include" expression ";"         -> include_file
      
?parameter: expression

?expression: term
           | term  "+" expression -> add
           | term  "-" expression -> sub
           | "null"               -> null_value

?term: factor
     | term "*" factor -> mul
     | term "/" factor -> div
     | term "%" factor -> mod

?factor: SIGNED_NUMBER          -> number
       | "-" factor             -> neg
       | string
       | "(" expression ")"
       | "$" VALNAME              -> call_variable
       | label                  -> call_predefined_variable
//       | "distance"             -> call_distance
       | label "(" [expression ("," expression)*] ")" -> call_function
          
?mapfunc: label "(" [expression] ("," [expression])* ")"
        
?mapobject: label ["[" parameter "]"] // [key]が指定されていない場合もNoneを返す

?label: CNAME
VALNAME: ("_"|LETTER|DIGIT) ("_"|LETTER|DIGIT)*

string: /'[^']*'/ -> remquote

%import common.CNAME
%import common.LETTER
%import common.DIGIT
%import common.SIGNED_NUMBER
%import common.WS
%import common.SH_COMMENT
%import common.CPP_COMMENT
%ignore WS
%ignore SH_COMMENT
%ignore CPP_COMMENT
%ignore "\""
