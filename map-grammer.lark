?start: expression ";"                   -> set_distance
      | "$" label "=" parameter ";"      -> set_variable
      | (mapobject ".")+ mapfunc ";"     -> map_element
      | "include" expression ";"         -> include_files
//     | SH_COMMENT -> ignore_comment
      
?parameter: expression

?expression: term
           | term  "+" expression -> add
           | term  "-" expression -> sub

?term: factor
     | factor "*" factor -> mul
     | factor "/" factor -> div
     | factor "%" factor -> mod

?factor: SIGNED_NUMBER -> number
       | string
       | "(" expression ")"
       | "$" label -> call_variable
       | "distance" -> call_distance
       | label "(" [expression ("," expression)*] ")" -> call_function
          
?mapfunc: label "(" [expression ("," expression)*] ")"
        
?mapobject: label ["[" parameter "]"]


?label: CNAME

string: /'[^']*'/ -> remquote

%import common.CNAME
%import common.SIGNED_NUMBER
%import common.WS
%import common.SH_COMMENT
%ignore WS
%ignore SH_COMMENT
